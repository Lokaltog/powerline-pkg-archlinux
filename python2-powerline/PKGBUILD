# Maintainer: Pablo Olmos de Aguilera C. pablo+aur at odac dot co

_pkgname=powerline
pkgname="python2-${_pkgname}"
pkgdesc='The ultimate statusline/prompt utility (stable package).'
pkgver=1.3.1
pkgrel=1
url="https://github.com/${_pkgname}/${_pkgname}"
license=('MIT')
arch=('any')
depends=('python2>=2.6')
optdepends=('python2-psutil: improved system information'
            'python2-pygit2: improved git support'
            'mercurial: improved mercurial support'
            'zsh: better shell prompt'
            'gvim: vim compiled with Python support')
makedepends=('git' 'python2-setuptools')
conflicts=('python2-powerline-git'
           'python-powerline-git'
           'python-powerline'
           'otf-powerline-symbols-git')
install="${_pkgname}.install"
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/${pkgver}.tar.gz"
        "${install}")
sha256sums=('00846fdd8257da115ec5df24b9b1aef00f0c9538260dc6ef786c19cbd4810d37'
            'af07852748ddba1544feee53fdf747d3dd163597f026719f8a6f80045d0c5425')

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	python2 setup.py install --root="${pkgdir}" --optimize=1

	# Fonts
	install -dm755 "${pkgdir}/etc/fonts/conf.d"
	install -Dm644 "font/PowerlineSymbols.otf" "${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf"
	install -Dm644 "font/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf"
	ln -s "../conf.avail/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf"

	# Vim Plugin
	install -Dm644 "powerline/bindings/vim/plugin/powerline.vim" "${pkgdir}/usr/share/vim/vimfiles/plugin/powerline.vim"

	# Zsh Plugin
	install -Dm644 "powerline/bindings/zsh/powerline.zsh" "${pkgdir}/usr/share/zsh/site-contrib/powerline.zsh"

	# Tmux Configuration
	install -Dm644 "powerline/bindings/tmux/powerline.conf" "${pkgdir}/usr/share/tmux/powerline.conf"

	# License
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
